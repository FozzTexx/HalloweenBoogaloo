100  REM BASIC WEEK 2: HALLOWEEN BOOGALOO
110  REM HTTP://REDDIT.COM/R/RETROBATTLESTATIONS
120  REM WRITTEN BY FOZZTEXX
130  REM CCE MC-1000 PORT BY ENSJO - 2013-10-18
140  REM ----
200  REM CLEAR SCREEN AND SETUP VARIABLES
210  TEXT : HOME : REM TEXT MODE
220  POKE 836,0: REM ENABLE PRINT AFTER LOADING
230  POKE 247,202: POKE 248,192: POKE 249,202: POKE 250,192: REM INVISIBLE CURSOR
240 AT$ =  CHR$ (27) + "=": REM FOR CURSOR POSITIONING
250 CL = 32:RW = 16: REM COLUMNS AND ROWS OF SCREEN
260  DIM SP(3),SP$(35,1),BT(1)
270 SZ = 0
300  REM LOAD BACKGROUND AND DRAW ON SCREEN
310  GOSUB 1510
320 NR = SR:WD = SC:WX =  - 2
330 X = 2:KF = X + WD: IF  RND (1) > 0.5 THEN X = CL - WD - 2:KF = 0
340 Y = RW - NR:KW = CL - WD - 4
350 SP = 0:D = 1: GOSUB 2010
500  REM LOAD SPRITES INTO VARIABLES
510 SP(1) = SZ:BT(0) = 1: GOSUB 1510:BC = SC
520 SP(2) = SZ:BT(1) = 2: GOSUB 1510
530 SP(3) = SZ:WT = 3: GOSUB 1510:WW = SC:WH = SR
600  REM ANIMATION
610 NB = 3: DIM BX(NB),BY(NB),BW(NB),BF(NB):WX =  - 2
620  FOR I = 1 TO NB
630 BX(I) =  INT ( RND (1) * (KW - BC)):BY(I) =  INT ( RND (1) * ((RW - 4 - NR / 2) / NB)) * NB + I + 1
640 BW(I) =  INT ( RND (1) * 2):BF(I) =  INT ( RND (1) * 9)
650  NEXT I
660  FOR I = 1 TO NB
670 Y =  INT (BF(I) / 3):X =  INT ((BF(I) / 3 - Y) * 3 + 0.05)
680 X = X + BX(I) + KF:Y = Y + BY(I):SP = BT(0):D = 0: GOSUB 2010
710 BW(I) = BW(I) + 1: IF BW(I) > 1 THEN BW(I) = 0
720 BF(I) =  INT ( RND (1) * 9)
730 Y =  INT (BF(I) / 3):X =  INT ((BF(I) / 3 - Y) * 3 + 0.05)
740 X = X + BX(I) + KF:Y = Y + BY(I):SP = BT(BW(I)):D = 1: GOSUB 2010
750  IF WX =  - 1 THEN SP = WT:D = 0:X = 0:Y = 0: GOSUB 2010:WX =  - 2
760  IF WX >  = 0 THEN SP = WT:D = 1:X = WX:Y = 0: GOSUB 2010:WX = WX - 1
770  IF WX =  - 2 AND WH + NR < RW AND  RND (1) * 50 < 2 THEN WX = CL - WW - 1
780  NEXT I
790  GOTO 660
1000  REM POSITION CURSOR AT X,Y
1010  PRINT AT$; CHR$ (Y); CHR$ (X + 32);
1020  POKE 910,0: REM ZERO POS() COUNTER.
1030  RETURN 
1500  REM LOAD A SPRITE FROM DATA INTO VARIABLES
1510 SR = 0:SC = 0
1520  READ S$
1530  IF  LEN (S$) = 0 THEN SZ = SZ + 1: RETURN 
1540 SP$(SZ,1) = S$:SR = SR + 1: IF  LEN (S$) > SC THEN SC =  LEN (S$)
1550 BL$ = "": FOR I = 1 TO  LEN (S$):BL$ = BL$ + " ": NEXT I
1560 SP$(SZ,0) = BL$
1570 SZ = SZ + 1
1580  GOTO 1520
2000  REM DRAW (D=1) OR ERASE (D=0) SPRITE SP AT X,Y
2010 J = SP(SP)
2020 S$ = SP$(J,D)
2030  IF  LEN (S$) = 0 THEN  RETURN 
2040  GOSUB 1010
2050  PRINT S$;
2060 Y = Y + 1
2070 J = J + 1
2080  GOTO 2020
5000  DATA "     +"
5010  DATA "    / \"
5020  DATA "   /(#)\"
5030  DATA "  +-----+"
5040  DATA "   ![+]!::::"
5050  DATA " /\!+-+!IIII"
5060  DATA "/..!! !!....\"
5080  DATA "!()!! !! [+]!"
5085  DATA ""
5160  DATA "/V\"
5165  DATA ""
5170  DATA "-V-"
5175  DATA ""
5180  DATA "     , "
5190  DATA "  ../(.. "
5200  DATA "   (\\ "
5210  DATA "   /;.) "
5220  DATA "=='/ <===<<< "
5230  DATA "   \..\ "
5240  DATA "     '' "
5245  DATA ""
